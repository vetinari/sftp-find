#!/usr/bin/perl
#
# sfind - search for files in an SFTP accessible directory hierarchy
#
# $Id$
#

=head1 NAME

sfind - search for files in an SFTP accessible directory hierarchy

=head1 SYNOPSIS

 sfind --key ~/.ssh/id_dsa user@remote:/path/to -type f -name '\.pl$' \
    -mtime +5 -print0

=head1 DESCRIPTION

B<sfind> lets you search for files in a remote location which is accessible
via L<ssh(1)> / L<sftp(1)>. It behaves similar to L<find(1)>, though some 
restrictions are given, because not all information is available via sftp's
L<stat(2)> method.

=head1 OPTIONS

The options are divided into two parts, the first one configures where to 
connect and how to authenticate at the remote system. The second part is
about selecting files and print them.

=head2 SSH Options

=over 4

=item --agent

Try authentication with ssh-agent

=item --key FILE

Uses FILE.pub as public key file and FILE as private key file to authenticate
at the remote system.

=item --pub-key FILE

Uses FILE as the public key for authentication. Note that this option requires
also a C<--sec-key> option and C<--key> must not be present.

=item --sec-key FILE

Uses FILE as the secret key for authentication. Note that this option requires
also a C<--pub-key> option and C<--key> must not be present.

=item --user NAME / --login NAME

The authenication will be tried as user NAME at the remote system. Note that
a user part in the remote system name will override this setting. If neither
--user / --login is given nor the user part exists the NAME will be set to 
whatever is in the environment variable C<USER>

=item --real-path

Files will be printed with full path instead of relative, uses L<sftp(1)>s
C<realpath> call.

=back

=head2 Filter Options

=over 4

=item -type TYPE

Selects files of the given type. Valid types are

=over 8

=item f 

regular B<f>iles

=item d 

B<d>irectories

=item p 

FIFOs (named B<p>ipes)

=item b

B<b>lock devices

=item c

B<c>haracter devices

=item l

symbolic B<l>inks

=item s

B<s>ockets

=back

=item -name REGEX

Only files matching the perl regex REGEX

=item -iname REGEX

Same as C<-name>, case insensitive.

=item -regex REGEX

Like C<-name>, but matches on the full C<path/file> instead of just the
file name.
NOTE: the path is the one given on the command line UNLESS the C<--real-path>
option is used

=item -uid NUMBER

Files' user id is equal to NUMBER

=item -gid NUMBER

Files' group id is equal to NUMBER

=item -empty 

Shortcut for C<-size 0>, note that it does not match empty directories

=item -mtime TIME

If time starts with a C<+>, files modification time older than TIME days.
A leading C<-> selects files newer than TIME days. Without any prefix: files
which are TIME days old.

NOTE: Currently this option behaves like C<-daystart> was given to L<find(1)>.

=item -atime TIME

Like C<-mtime>, only for atime (access time).

=item -mmin TIME

Like C<-mtime>, but in minutes

=item -amin TIME

Like C<-atime>, but in minutes

=item -size SIZE

If SIZE starts with a C<+>, files with a size greater than SIZE are selected.
A leading C<-> selects files smaller than SIZE. Without any prefix: files
which are exactly SIZE bytes.

=back

=head2 Tree Traversal Options

=over 4

=item -maxdepth DEPTH

Only descend DEPTH levels deep.

=item -depth

Does a depth first search, i.e. directories are descended as soon as one is 
found. Default is to process contents first, then descend.

=back

=head2 Output Options

=over 4

=item -print

Prints the name of all matching files separated by a newline C<\n>, this 
is the default if no other output option is given.

=item -print0

Prints the names of all matching files spearated by a C<\0>, useful for files
which may contain spaces or newlines. Use with L<xargs(1)>'s C<-0> option.

=item -printf FORMAT

Prints the wanted information in the given FORMAT

=item -ls

A L<ls(1)> like output, due to missing stat info not compatible to L<find(1)>'s
C<-ls> option. This is a shortcut for C<-printf '%M %u %g %s %TI %p%l\n'>

=back

=head2 Unsupported options from find

=over 4 

=item -ctime

=item -cmin

=item -user

=item -group

=back

=head1 SEE ALSO

L<ssh(1)>, L<sftp(1)>, L<find(1)>

=head1 DOWNLOAD

L<http://ankh-morp.org/code/sfind/sfind> or
L<SVN|http://svn.ankh-morp.org:8080/tools/sfind/>

=head1 AUTHOR

Hanno Hecker E<lt>vetinari@ankh-morp.orgE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2012 by Hanno Hecker; all rights reserved.

This script is free software; you can redistribute it and/or modify it
under the same terms as Perl itself, either Perl version 5.8.0 or, at
your option, any later version of Perl 5 you may have available.

=cut

use strict;
use warnings;
use Net::SSH2;
use Getopt::Long  qw{ :config require_order };
use POSIX qw{ S_ISBLK S_ISCHR S_ISDIR S_ISFIFO S_ISREG 
              strftime 
            };

my ($remote_user, $remote_host, $key_file, $pub_key, $sec_key, $use_ssh_agent);
my $use_realpath;
my $script = $0;
$script =~ s#.*/##;

GetOptions(
        "user|login=s" => \$remote_user,
        "key=s" => \$key_file,
        "agent" => \$use_ssh_agent,
        "pub-key=s" => \$pub_key,
        "sec-key=s" => \$sec_key,
        "real-path" => \$use_realpath,
    )
    or die usage();
if (($sec_key and not $pub_key) or ($pub_key and not $sec_key)) {
    die "$script: one of --sec-key and --pub-key is missing\n";
}
if ($sec_key and $key_file) {
    die "$script: cannot give --sec-key and --key at the same time\n";
}
if ($key_file) {
    $sec_key = $key_file;
    $pub_key = "$key_file.pub";
}
if (!$remote_user) {
    $remote_user = $ENV{USER};
}

my $user_at_host = shift @ARGV
  or die usage();

my ($user, $host, $base_dir);
if ($user_at_host =~ s/^(([^@]+)\@)?([\w\d\.\-]+)://) {
    if (defined $2 and $2 ne "") {
        $remote_user = $2;
    }
    $remote_host = $3;
    $base_dir = $user_at_host;
    if ($base_dir eq "") {
        $base_dir = ".";
    }
}

my @criteria    = ();
my $eol         = "\n";
my $printf      = '%p';
my $ls_mode     = 0;
my $max_depth   = undef;
my $depth_first = 0;
my $negate      = 0;

my %options = (

        '-not' => {
            option => sub {
                $negate = 1;
            },
        },

        '-type' => {
            option => sub {
                my $v = shift @ARGV;
                if (!defined $v or $v =~ /^-/) {
                    die "$script: option -type requires an argument\n";
                }
                my $test;
                if    ($v eq 'f') { $test = \&S_ISREG; }
                elsif ($v eq 'd') { $test = \&S_ISDIR; }
                elsif ($v eq 'b') { $test = \&S_ISBLK; }
                elsif ($v eq 'c') { $test = \&S_ISCHR; }
                elsif ($v eq 'p') { $test = \&S_ISFIFO; }
                elsif ($v eq 'l') { $test = \&S_ISLNK; }
                elsif ($v eq 's') { $test = \&S_ISSOCK; }
                else {
                    die "$script: unknown argument '$v' for -type\n";
                }
                push @criteria, ['-type', $test, undef, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $test, $chr, $negate) = @_;
                my $ret = $test->($entry->{mode});
                return ($negate ? !$ret : $ret);
            },
        },

        '-name' => {
            option => sub {
                my $v = shift @ARGV;
                if (!defined $v or $v =~ /^-/) {
                    die "$script: option -name requires an argument\n";
                }
                $v = qr/$v/ or die "$script: ivalid regex for -name\n";
                push @criteria, ['-name', $v, undef, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $test, $args, $negate) = @_;
                if ($negate) { $entry->{name} !~ $test; }
                else         { $entry->{name} =~ $test; }
            },
        },

        '-iname' => {
            option => sub {
                my $v = shift @ARGV;
                if (!defined $v or $v =~ /^-/) {
                    die "$script: option -iname requires an argument\n";
                }
                $v = qr/$v/i or die "$script: ivalid regex for -iname\n";
                push @criteria, ['-iname', $v, undef, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $test, $args, $negate) = @_;
                if ($negate) { $entry->{name} !~ $test; }
                else         { $entry->{name} =~ $test; }
            },
        },

        '-regex' => {
            option => sub {
                my $v = shift @ARGV;
                if (!defined $v or $v =~ /^-/) {
                    die "$script: option -iname requires an argument\n";
                }
                $v = qr/$v/ or die "$script: ivalid regex for -regex\n";
                push @criteria, ['-regex', $v, undef, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $test, $args, $negate) = @_;
                my $file = $entry->{path}.'/'.$entry->{name};
                if ($negate) { $file !~ $test; }
                else         { $file =~ $test; }
            },

        },

        '-uid' => {
            option => sub {
                my $v = shift @ARGV;
                if (!defined $v or $v =~ /^-/) {
                    die "$script: option -uid requires an argument\n";
                }
                push @criteria, ['-uid', $v, undef, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $test, $args, $negate) = @_;
                my $ret = ($entry->{uid} == $test);
                return $negate ? !$ret : $ret;
            },
        },

        '-gid' => {
            option => sub {
                my $v = shift @ARGV;
                if (!defined $v or $v =~ /^-/) {
                    die "$script: option -gid requires an argument\n";
                }
                push @criteria, ['-gid', $v, undef, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $test, $args, $negate) = @_;
                my $ret = ($entry->{gid} == $test);
                return $negate ? !$ret : $ret;
            },
        },

        '-empty' => {
            option => sub {
                push @criteria, ['-empty', undef, undef, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $test, $args, $negate) = @_;
                return $negate ? $entry->{size} : !$entry->{size};
            },
        },

        '-mtime' => {
            option => sub {
                my ($cmp, $val) = check_cmp_option('-mtime', shift @ARGV);

                push @criteria, ['-mtime', ref_time_start($val, 86400), $cmp, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $ref, $cmp, $negate) = @_;
                my $m = $entry->{mtime} - ($entry->{mtime} % 86400);

                my $ret;
                if    ($cmp eq '-') { $ret = ($m > $ref); }
                elsif ($cmp eq '+') { $ret = ($m < $ref); }
                else                { $ret = ($m == $ref); }

                return $negate ? !$ret : $ret;
            },
        },

        '-atime' => {
            option => sub {
                my ($cmp, $val) = check_cmp_option('-atime', shift @ARGV);

                push @criteria, ['-atime', ref_time_start($val, 86400), $cmp, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $ref, $cmp, $negate) = @_;
                my $m = $entry->{atime} - ($entry->{atime} % 86400);
                
                my $ret;
                if    ($cmp eq '-') { $ret = ($m > $ref); }
                elsif ($cmp eq '+') { $ret = ($m < $ref); }
                else                { $ret = ($m == $ref); }

                return $negate ? !$ret : $ret;
            },
        },

        '-mmin' => {
            option => sub {
                my ($cmp, $val) = check_cmp_option('-mmin', shift @ARGV);

                push @criteria, ['-mmin', ref_time_start($val, 60), $cmp, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $ref, $cmp, $negate) = @_;
                my $m = $entry->{mtime} - ($entry->{mtime} % 60);

                my $ret;
                if    ($cmp eq '-') { $ret = ($m > $ref); }
                elsif ($cmp eq '+') { $ret = ($m < $ref); }
                else                { $ret = ($m == $ref); }

                return $negate ? !$ret : $ret;
            },
        },

        '-amin' => {
            option => sub {
                my ($cmp, $val) = check_cmp_option('-amin', shift @ARGV);

                push @criteria, ['-amin', ref_time_start($val, 60), $cmp, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $ref, $cmp, $negate) = @_;
                my $m = $entry->{atime} - ($entry->{atime} % 60);

                my $ret;
                if    ($cmp eq '-') { $ret = ($m > $ref); }
                elsif ($cmp eq '+') { $ret = ($m < $ref); }
                else                { $ret = ($m == $ref); }

                return $negate ? !$ret : $ret;
            },
        },

        '-size' => {
            option => sub {
                my ($cmp, $val) = check_cmp_option('-size', shift @ARGV);

                push @criteria, ['-size', $val, $cmp, $negate];
                $negate = 0;
            },
            filter => sub {
                my ($entry, $ref, $cmp, $negate) = @_;

                my $ret;
                if    ($cmp eq '-') { $ret = ($entry->{size} < $ref); }
                elsif ($cmp eq '+') { $ret = ($entry->{size} > $ref); }
                else                { $ret = ($entry->{size} == $ref); }
                
                return $negate ? !$ret : $ret;
            },
        },

        '-maxdepth' => {
            option => sub {
                my $v = shift @ARGV;
                if (!defined $v or $v =~ /^-/) {
                    die "$script: option -maxdepth requires an argument\n";
                }
                $max_depth = $v;
            },
        },

        '-depth' => {
            option => sub { $depth_first = 1; },
        },

        '-print' => {
            option => sub {
                $eol     = "\n";
                $printf  = '%p';
                $ls_mode = 0;
            },
        },

        '-print0' => {
            option => sub {
                $eol     = "\0";
                $printf  = '%p';
                $ls_mode = 0;
            },
        },

        '-printf' => {
            option => sub {
                $eol     = "";
                $printf  = shift @ARGV;
                die "$script: option -printf requires an argument\n"
                    unless defined $printf;
                $ls_mode = 0;
            },
        },

        '-ls' => {
            option => sub {
                $eol     = "\n";
                $printf  = '%M %u %g %s %TI %p%l';
                $ls_mode = 1;
            },
        },

        '-ctime' => {
            option => sub { die "$script: unsupported option -ctime\n"; }
        },
        '-cmin' => {
            option => sub { die "$script: unsupported option -cmin\n"; }
        },
        '-user' => {
            option => sub { die "$script: unsupported option -user, use -uid\n"; }
        },
        '-group' => {
            option => sub { die "$script: unsupported option -group, use -gid\n"; }
        },
    );

while (@ARGV) {
    my $opt = shift @ARGV;
    die "$script: unknown argument $opt\n"
        unless exists $options{$opt};

    $options{$opt}->{option}();
}

my $ssh = Net::SSH2->new();
$ssh->connect($remote_host)
  or die "$script: failed to connect to $remote_host $!\n";
unless (authenticate($remote_user, $pub_key, $sec_key)) {
    die "$script: auth failed...\n";
}

my $sftp = $ssh->sftp()
    or die "$script: sftp() failed: ", ssh_errmsg() ,"\n";

if ($use_realpath) {
    my $real_base = $sftp->realpath($base_dir);
    die "$script: failed to get realpath of remote dir $base_dir\n"
        unless defined $real_base;
    $base_dir = $real_base;
}

find($base_dir, 0);
exit 0;

sub authenticate {
    my ($user, $pub, $sec) = @_;
    if ($use_ssh_agent) {
        return 1
            if $ssh->auth_agent($user);
    }
    if (!defined $pub) {
        unless ($use_ssh_agent) {
            return 1
                if $ssh->auth_agent($user);
        }
        my @auth_list = $ssh->auth_list($user);
        foreach my $type (@auth_list) {
            if ($type eq 'publickey') {
                foreach my $key (qw{ id_dsa id_rsa }) {
                    my $file = "$ENV{HOME}/.ssh/$key";
                    if (-e $file) {
                        return 1
                            if $ssh->auth_publickey($user, "$file.pub", $file);
                    }
                }
            }
            elsif ($type eq 'password') {
                return 1
                    if $ssh->auth_password($user, get_password($ssh, $user));
            }
            elsif ($type eq 'keyboard-interactive') {
                return 1
                    if $ssh->auth_keyboard($user, \&get_password);
            }
            else {
                die "$script: FIXME: auth with method $type\n";
            }
        }
        return 0;
    }
    else {
        return $ssh->auth_publickey($user, $pub, $sec);
    }
}

sub get_password {
    my $ssh  = shift;
    my $user = shift;

    require Term::ReadKey;

    print STDERR "[$script] $user\@$remote_host\'s password: ";
    Term::ReadKey::ReadMode('noecho');
    chomp(my $value = Term::ReadKey::ReadLine(0));
    Term::ReadKey::ReadMode('normal');

    return $value;
}

sub find {
    my $name  = shift;
    my $depth = shift;
    my $dir   = $sftp->opendir($name);
    unless ($dir) {
        warn "$script: opendir($name) failed: ",ssh_errmsg(),"\n";
        return;
    }
    my @sub_dirs;
    while (my $entry = $dir->read()) {
        if (S_ISDIR($entry->{mode})) {
            next if $entry->{name} =~ /^\.\.?$/;

            if (!defined $max_depth or $depth < $max_depth) {
                if ($depth_first) {
                    find("$name/".$entry->{name}, $depth + 1);
                }
                else {
                    push @sub_dirs, "$name/".$entry->{name};
                }
            }
        }

        $entry->{path}  = $name;
        $entry->{depth} = $depth;
        if (filter($entry)) {
            print_entry($entry);
        }
    }

    foreach (@sub_dirs) {
        find($_, $depth + 1);
    }
}

sub print_entry {
    my $entry = shift;
    my @chars = split //, $printf;
    my $out   = "";
    my $fname = $entry->{path}."/".$entry->{name};
    $fname =~ tr{/}{/}s;

    while (@chars) {
        my $chr = shift @chars;
        if ($chr eq '%') {
            $chr = shift @chars;
            if ($chr eq '%') {
                $out .= '%';
            }
            elsif ($chr eq 'p') {
                $out .= $fname;
            }
            elsif ($chr eq 'l') {
                my $target = $sftp->readlink($fname);
                if (defined $target) {
                    $out .= " -> $target";
                }
            }
            elsif ($chr eq 'f') {
                $out .= $entry->{name};
            }
            elsif ($chr eq 'h') {
                $out .= $entry->{path};
            }
            elsif ($chr eq 's') {
                $out .= $entry->{size};
            }
            elsif ($chr eq 'm') {
                $out .= sprintf "%o", $entry->{mode};
            }
            elsif ($chr eq 'M') {
                $out .= get_ls_mode($entry);
            }
            elsif ($chr eq 'a') {
                $out .= $entry->{atime};
            }
            elsif ($chr eq 'u' or $chr eq 'U') {
                $out .= $entry->{uid};
            }
            elsif ($chr eq 'g' or $chr eq 'G') {
                $out .= $entry->{gid};
            }
            elsif ($chr eq 't') {
                $out .= $entry->{mtime};
            }
            elsif ($chr eq 'd') {
                $out .= $entry->{depth};
            }
            elsif ($chr eq 'T') {
                $chr = shift @chars;
                if ($chr eq '@') {
                    $out .= $entry->{mtime};
                }
                elsif ($chr eq 'I') {
                    $out .= strftime('%Y-%m-%d %H:%M:%S', localtime($entry->{mtime}));
                }
            }
        }
        elsif ($chr eq '\\') {
            $chr = shift @chars;
            eval "\$chr = \"\\$chr\";";
            $out .=  $chr;
        }
        else {
            $out .= $chr;
        }
    }
    
    print $out, $eol;
}

# FIXME: values taken from linux:
sub S_ISSOCK {
    my $mode = shift;
    ($mode & 0140000) == 0140000;
}

sub S_ISLNK {
    my $mode = shift;
    ($mode & 0120000) == 0120000;
}


sub get_ls_mode {
    my $entry = shift;
    my $str   = '';

    if    (S_ISDIR($entry->{mode}))  { $str .= "d"; }
    elsif (S_ISREG($entry->{mode}))  { $str .= "-"; }
    elsif (S_ISFIFO($entry->{mode})) { $str .= "p"; }
    elsif (S_ISCHR($entry->{mode}))  { $str .= "c"; }
    elsif (S_ISBLK($entry->{mode}))  { $str .= "b"; }
    elsif (S_ISLNK($entry->{mode}))  { $str .= "l"; }
    elsif (S_ISSOCK($entry->{mode})) { $str .= "s"; }
    else  {
        warn "$script: unknown file type: ",
            sprintf "FILE=%s/%s MODE=%o %x\n",
                $entry->{path}, $entry->{name}, 
                    ($entry->{mode} &~ 07777), 
                    ($entry->{mode} &~ 07777);
        $str .= "U";
    }

    my $mode = $entry->{mode} & 07777;
    $str .= ($mode & 0400) ? "r" : "-";
    $str .= ($mode & 0200) ? "w" : "-";
    if    ($mode & 04000 and $mode & 0100) { $str .= "s"; }
    elsif ($mode & 0100)                   { $str .= "x"; }
    elsif ($mode & 04000)                  { $str .= "S"; }
    else                                   { $str .= "-"; }

    $str .= ($mode & 040) ? "r" : "-";
    $str .= ($mode & 020) ? "w" : "-";
    if    ($mode & 02000 and $mode & 010) { $str .= "s"; }
    elsif ($mode & 010)                   { $str .= "x"; }
    elsif ($mode & 02000)                 { $str .= "S"; }
    else                                  { $str .= "-"; }

    $str .= ($mode & 04) ? "r" : "-";
    $str .= ($mode & 02) ? "w" : "-";
    if    ($mode & 01000 and $mode & 01) { $str .= "t"; }
    elsif ($mode & 01)                   { $str .= "x"; }
    elsif ($mode & 01000)                { $str .= "T"; }
    else                                 { $str .= "-"; }

    return $str;
}

sub ref_time_start {
    my ($val, $time) = @_;
    return $^T - ($^T % $time) - ($val * $time);
}

sub check_cmp_option {
    my $opt   = shift;
    my $value = shift;
    die "$script: option $opt requires an argument\n"
        unless defined $value;
    $value =~ /^(\+|-)?(\d+)$/
        or die "$script: illegal value for $opt option\n";
    return (defined $1 ? $1 : "", $2);
}

sub filter {
    my $entry = shift;
    foreach my $crit (@criteria) {
        my @data = @$crit;
        my $opt = shift @data;
        $options{$opt}->{filter}->($entry, @data)
            or return 0;
    }
    return 1;
}

sub usage {
    return <<_END;
$script: Usage: $script [SSH_OPTIONS] [user\@]host:[DIR] [FIND_OPTIONS]
_END
}

sub ssh_errmsg {
    ($ssh->error)[2];
}

# vim: ts=4 sw=4 expandtab syn=perl
